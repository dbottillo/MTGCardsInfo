buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.20.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'findbugs'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

android {
    compileSdkVersion 23
    buildToolsVersion '22.0.1'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 26
        versionName "2.5.0"
        applicationId "com.dbottillo.mtgsearchfree"
    }

    signingConfigs {
        debugConfig {
            keyAlias 'androiddebugkey'
            storeFile file('../debug.keystore')
            keyPassword 'android'
            storePassword 'android'
        }

        releaseConfig {
            keyAlias 'dbottillo'
            storeFile file('../dbottillo.keystore')
            keyPassword ''
            storePassword ''
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "_dev"
            signingConfig signingConfigs.debugConfig
            debuggable true
            buildConfigField 'int', 'DATABASE_VERSION', '9'
            buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.releaseConfig
            buildConfigField 'int', 'DATABASE_VERSION', '9'
            buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        }
    }
}

task readPasswordFromInput << {
    def console = System.console()
    if (console == null)
        throw new IllegalStateException('no console available, use --no-daemon flag')

    ext.keystorePassword = console.readLine('\n\n\n> Enter keystore password: ')

    android.signingConfigs.releaseConfig.storePassword = ext.keystorePassword
    android.signingConfigs.releaseConfig.keyPassword = ext.keystorePassword
}

/*tasks.whenTaskAdded { task ->
    if (task.name == 'assembleHearthstoneRelease' || task.name == 'assembleMagicRelease')
        task.dependsOn readPasswordFromInput
}*/

tasks.whenTaskAdded { task ->
    if (task.name == 'validateReleaseConfigSigning') {
        task.dependsOn readPasswordFromInput
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.google.android.gms:play-services-analytics:7.8.0'
    compile 'com.readystatesoftware.sqliteasset:sqliteassethelper:2.0.1'
    compile 'com.github.castorflex.smoothprogressbar:library:0.3.3'
    compile 'com.squareup.picasso:picasso:2.3.4'
    compile "com.android.support:appcompat-v7:23.0.1"
    compile 'com.android.support:cardview-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'

    // Crashlytics Kit
    compile('com.crashlytics.sdk.android:crashlytics:2.4.0@aar') {
        transitive = true
    }

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}

/*task findbugs(type: FindBugs) {
    ignoreFailures = false
    effort = "default"
    reportLevel = "medium"
    classes = files("${project.rootDir}/app/build/intermediates/classes")
    source = fileTree('src/main/java/')
    classpath = files()
    reports {
        xml.enabled = false
        html.enabled = true
        html {
            destination "$project.buildDir/findbugs/findbugs-output.html"
        }
    }
}*/
