buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'crashlytics'
repositories {
    maven { url 'http://download.crashlytics.com/maven' }
}


android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
    }

    signingConfigs {
        debugConfig {
            keyAlias 'androiddebugkey'
            storeFile file('../debug.keystore')
            keyPassword 'android'
            storePassword 'android'
        }

        releaseConfig {
            keyAlias 'dbottillo'
            storeFile file('../dbottillo.keystore')
            keyPassword ''
            storePassword ''
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "_dev"
            signingConfig signingConfigs.debugConfig
            debuggable true
        }
        release {
            debuggable false
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.releaseConfig
        }
    }

    productFlavors {
        magicFree {
            applicationId "com.dbottillo.mtgsearchfree"
            buildConfigField 'boolean', 'magic', 'true'
            buildConfigField 'int', 'DATABASE_VERSION', '3'
            versionCode 8
            versionName "1.6"
        }

        magicPaid {
            applicationId "com.dbottillo.mtgsearch"
            buildConfigField "Boolean", "magic", "true"
            buildConfigField 'int', 'DATABASE_VERSION', '3'
            versionCode 7
            versionName "1.5"
        }

        hearthstoneFree {
            applicationId "com.dbottillo.hssearchfree"
            buildConfigField "Boolean", "magic", "false"
            buildConfigField 'int', 'DATABASE_VERSION', '1'
            versionCode 1
            versionName "1.0"
        }

        hearthstonePaid {
            applicationId "com.dbottillo.hssearch"
            buildConfigField "Boolean", "magic", "false"
            buildConfigField 'int', 'DATABASE_VERSION', '1'
            versionCode 1
            versionName "1.0"
        }
    }
}

task readPasswordFromInput << {
    def console = System.console()
    if (console == null)
        throw new IllegalStateException('no console available, use --no-daemon flag')

    ext.keystorePassword = console.readLine('\n\n\n> Enter keystore password: ')

    android.signingConfigs.releaseConfig.storePassword = ext.keystorePassword
    android.signingConfigs.releaseConfig.keyPassword = ext.keystorePassword
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleHearthstoneFreeRelease' || task.name == 'assembleMagicFreeRelease')
        task.dependsOn readPasswordFromInput
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.google.android.gms:play-services:5.0.77'
    compile 'com.readystatesoftware.sqliteasset:sqliteassethelper:+'
    compile 'com.github.castorflex.smoothprogressbar:library:0.3.3'
    compile 'com.crashlytics.android:crashlytics:1.+'

}
