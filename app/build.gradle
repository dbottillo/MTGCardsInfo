apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply from: '../config-android.gradle'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    keystoreProperties['RELEASE_KEY_PASSWORD'] = ""
    keystoreProperties['RELEASE_STORE_PASSWORD'] = ""
}

android {
    defaultConfig {
        applicationId 'com.dbottillo.mtgsearchfree'
        testInstrumentationRunner "com.dbottillo.mtgsearchfree.TestMTGRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true
    }

    signingConfigs {
        debugConfig {
            keyAlias 'androiddebugkey'
            storeFile file('../debug.keystore')
            keyPassword 'android'
            storePassword 'android'
        }

        releaseConfig {
            keyAlias 'upload'
            storeFile file('../upload-keystore.jks')
            keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
            storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "_dev"
            signingConfig signingConfigs.debugConfig
            debuggable true
            testCoverageEnabled false
            ext.alwaysUpdateBuildId = false
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.releaseConfig
            testCoverageEnabled false
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        test.resources.srcDirs += ['src/test/resources', 'src/debug/res/raw']
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        xmlReport false
        ignoreTestSources true
        checkDependencies true
        lintConfig file("$rootDir/config/lint/lint.xml")
    }
}

dependencies {
    implementation project(':feature_about')
    implementation project(':feature_settings')
    implementation project(':feature_release_note')
    implementation project(':feature_life_counter')
    implementation project(':feature_cards_lucky')
    implementation project(':feature_decks')
    implementation project(':feature_home')
    implementation project(':feature_sets')
    implementation project(':feature_base_cards')
    implementation project(':feature_cards_saved')
    implementation project(':feature_search')
    implementation project(':feature_cards')
    implementation project(':feature_debug')
    implementation project(':data_cards')
    implementation project(':data_sets')
    implementation project(':data_decks')
    implementation project(':core')
    implementation project(':network')

    core()
    di()
    image()
    ui()
    monitoring()
    debug()
    test()
    robolectric()
}

apply plugin: 'com.google.gms.google-services'