buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
    }

    dependencies {
        apply from: 'dependencies-versions.gradle'
        apply from: 'dependencies-all.gradle'

        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath 'com.google.gms:google-services:4.3.8'
        classpath "com.google.firebase:firebase-crashlytics-gradle:$versions.crashlyticsPlugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.39.0"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:5.14.2"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.17.1"
}

allprojects {
    repositories {
        maven { url "https://maven.google.com" }
        maven { url "https://repo.maven.apache.org/maven2" }
    }

    dependencies { apply from: "$rootProject.rootDir/dependencies-groups.gradle" }
}

apply plugin: 'com.diffplug.spotless'
spotless {
    kotlin {
        target 'src/**/*.kt'
        ktlint('0.37.2')
    }
}

subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'
    detekt {
        config = files("$rootDir/config/detekt/config.yml")
        baseline = file("$projectDir/config/detekt/baseline.xml")

        reports {
            html {
                enabled = true
                destination = file("$projectDir/build/detekt.html")
            }
        }

    }

    apply plugin: 'pmd'
    apply plugin: 'checkstyle'
}

apply plugin: 'com.github.ben-manes.versions'
dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'atlassian', 'm1', 'm2', 'm3'].any { qualifier ->
                selection.candidate.version.toLowerCase().contains(qualifier)
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

def checkstyleConfigDir = new File("$rootDir/config/checkstyle")
task checkstyle(type: Checkstyle) {
    configFile = new File(checkstyleConfigDir, "checkstyle.xml")
    configProperties.checkstyleConfigDir = checkstyleConfigDir
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}


task pmd(type: Pmd) {

    description 'Run pmd'
    group 'verification'

    ruleSetFiles = files("./config/pmd/pmd-ruleset.xml")
    ruleSets = []
    source = fileTree('src/main/java')

    reports {
        xml.enabled = false
        html.enabled = true
    }
}

apply from: 'githooks.gradle'
