buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
    }

    dependencies {
        apply from: 'dependencies-versions.gradle'
        apply from: 'dependencies-all.gradle'

        classpath 'com.android.tools.build:gradle:7.1.0'
        classpath 'com.google.gms:google-services:4.3.10'
        classpath "com.google.firebase:firebase-crashlytics-gradle:$versions.crashlyticsPlugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.41.0"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:6.2.0"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
}

allprojects {
    repositories {
        maven { url "https://maven.google.com" }
        maven { url "https://repo.maven.apache.org/maven2" }
    }

    dependencies { apply from: "$rootProject.rootDir/dependencies-groups.gradle" }
}

apply plugin: 'com.diffplug.spotless'
spotless {
    kotlin {
        target 'src/**/*.kt'
        ktlint('0.37.2')
    }
}

subprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'
    detekt {
        autoCorrect = true
        parallel = true
        config = files("$rootDir/config/detekt/config.yml")
        baseline = file("$projectDir/config/detekt/baseline.xml")
        reports {
            xml.required.set(false)
            html.required.set(true)
            html.outputLocation.set(file("$projectDir/build/reports/detekt.html"))
            sarif.required.set(false)
        }
    }
}

apply plugin: 'com.github.ben-manes.versions'
dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'atlassian', 'm1', 'm2', 'm3'].any { qualifier ->
                selection.candidate.version.toLowerCase().contains(qualifier)
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

apply from: 'githooks.gradle'
